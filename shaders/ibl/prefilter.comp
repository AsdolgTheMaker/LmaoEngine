#version 460

layout(local_size_x = 16, local_size_y = 16) in;

layout(set = 0, binding = 0) uniform samplerCube envMap;
layout(set = 0, binding = 1, rgba16f) uniform writeonly imageCube prefilteredMap;

layout(push_constant) uniform PrefilterPC {
    float roughness;
    uint mipSize;
};

const float PI = 3.14159265359;

float radicalInverse(uint bits) {
    bits = (bits << 16u) | (bits >> 16u);
    bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
    bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
    bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
    bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
    return float(bits) * 2.3283064365386963e-10;
}

vec2 hammersley(uint i, uint N) {
    return vec2(float(i) / float(N), radicalInverse(i));
}

vec3 importanceSampleGGX(vec2 xi, vec3 N, float roughness) {
    float a = roughness * roughness;
    float phi = 2.0 * PI * xi.x;
    float cosTheta = sqrt((1.0 - xi.y) / (1.0 + (a * a - 1.0) * xi.y));
    float sinTheta = sqrt(1.0 - cosTheta * cosTheta);

    vec3 H = vec3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);

    vec3 up = abs(N.z) < 0.999 ? vec3(0.0, 0.0, 1.0) : vec3(1.0, 0.0, 0.0);
    vec3 tangent = normalize(cross(up, N));
    vec3 bitangent = cross(N, tangent);

    return normalize(tangent * H.x + bitangent * H.y + N * H.z);
}

vec3 cubeDir(int face, vec2 uv) {
    vec2 st = uv * 2.0 - 1.0;
    switch (face) {
        case 0: return normalize(vec3( 1.0, -st.y, -st.x));
        case 1: return normalize(vec3(-1.0, -st.y,  st.x));
        case 2: return normalize(vec3( st.x,  1.0,  st.y));
        case 3: return normalize(vec3( st.x, -1.0, -st.y));
        case 4: return normalize(vec3( st.x, -st.y,  1.0));
        case 5: return normalize(vec3(-st.x, -st.y, -1.0));
    }
    return vec3(0.0);
}

void main() {
    ivec2 pos = ivec2(gl_GlobalInvocationID.xy);
    int face = int(gl_GlobalInvocationID.z);

    if (pos.x >= int(mipSize) || pos.y >= int(mipSize) || face >= 6) return;

    vec2 uv = (vec2(pos) + 0.5) / float(mipSize);
    vec3 N = cubeDir(face, uv);
    vec3 R = N;
    vec3 V = R;

    const uint SAMPLE_COUNT = 1024u;
    float totalWeight = 0.0;
    vec3 prefilteredColor = vec3(0.0);

    for (uint i = 0u; i < SAMPLE_COUNT; i++) {
        vec2 xi = hammersley(i, SAMPLE_COUNT);
        vec3 H = importanceSampleGGX(xi, N, roughness);
        vec3 L = normalize(2.0 * dot(V, H) * H - V);

        float NdotL = max(dot(N, L), 0.0);
        if (NdotL > 0.0) {
            prefilteredColor += texture(envMap, L).rgb * NdotL;
            totalWeight += NdotL;
        }
    }

    prefilteredColor /= max(totalWeight, 0.001);
    imageStore(prefilteredMap, ivec3(pos, face), vec4(prefilteredColor, 1.0));
}
